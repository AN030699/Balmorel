Equations
   QHYDROGEN_HGEHTOH2(Y,AAA,G,S,T)    'Link heat and electricity demand in hydrogen production'
   QHYDROGEN_H2TOEH(Y,AAA,G,S,T)      'Link heat and electricity in technologies consuming hydrogen'
   QHYDROGEN_GCH4KT(Y,AAA,G,S,T)      'Hydrogen production of steam reforming plants restricted by capacity'
   QHYDROGEN_STOVOL(Y,AAA,G,S,T)      'Dynamic equation hydrogen storage'
   QHYDROGEN_STOMAXCON(Y,AAA,G,S,T)   'Maximum hydrogen storage capacity'
   QHYDROGEN_STOMAXLOAD(Y,AAA,G,S,T)  'Maximum loading capacity of hydrogen storage'
   QHYDROGEN_STOMAXUNLD(Y,AAA,G,S,T)  'Maximum unloading capacity of hydrogen storage'
   QHYDROGEN_EQ(Y,RRR,S,T)            'Hydrogen production equal demand'
   QHYDROGEN_EQ_AREA(Y,AAA,S,T)       'Hydrogen production equals demand at an areal level'
   QHYDROGENGKE_UP_ADD(Y,AAA,G,S,T)   'Capacity adjustment constraint on electricity generation from biogas upgraded plants (MW)'
   QBIOMETHANE_EQ                     'Biomethane production equals demand on international level in each year'
   QGKBIOMETHANE_UP                   'Maximum generation of biomethane (MW)'
;



QHYDROGEN_HGEHTOH2(IAGK_HASORPOT(IY411,IA,IHYDROGEN_GEHTOH2),IS3,T)$GDATA(IHYDROGEN_GEHTOH2,'GDCB')..

       VGE_T(IY411,IA,IHYDROGEN_GEHTOH2,IS3,T) =E= VGH_T(IY411,IA,IHYDROGEN_GEHTOH2,IS3,T)*GDATA(IHYDROGEN_GEHTOH2,'GDCB');


QHYDROGEN_H2TOEH(IAGK_HASORPOT(IY411,IA,IHYDROGEN_GH2TOEH),IS3,T)$GDATA(IHYDROGEN_GH2TOEH,'GDCB')..

       VGE_T(IY411,IA,IHYDROGEN_GH2TOEH,IS3,T) =E= VGH_T(IY411,IA,IHYDROGEN_GH2TOEH,IS3,T)*GDATA(IHYDROGEN_GH2TOEH,'GDCB');

* Hydrogen gas storage, dynamic balance for weekly time horizon:
* Start and end filling degree in each week fixed to values obtained
* from yearly simulation


* Hydrogen production equal demand on region level:
* Prod + OutputStorage - InputStorage = DemandTransport + DemandPower+DemandBioRefinery (other exogenous demand)

QHYDROGEN_EQ(IY411,IR,IS3,T)..
   SUM(IAGK_HASORPOT(IY411,IA,IHYDROGEN_GETOH2)$(RRRAAA(IR,IA)),VGE_T(IY411,IA,IHYDROGEN_GETOH2,IS3,T)*GDATA(IHYDROGEN_GETOH2,'GDFE')) +
   SUM(IAGK_HASORPOT(IY411,IA,IHYDROGEN_GEHTOH2)$(RRRAAA(IR,IA)),VGE_T(IY411,IA,IHYDROGEN_GEHTOH2,IS3,T)*GDATA(IHYDROGEN_GEHTOH2,'GDFE'))+
   SUM(IAGK_HASORPOT(IY411,IA,IHYDROGEN_GCH4TOH2)$(RRRAAA(IR,IA)),VHYDROGEN_GH2_T(IY411,IA,IHYDROGEN_GCH4TOH2,IS3,T)) +
   SUM(IAGK_HASORPOT(IY411,IA,IHYDROGEN_GH2STO)$(RRRAAA(IR,IA)),VHYDROGEN_GH2_T(IY411,IA,IHYDROGEN_GH2STO,IS3,T))
   - SUM((IA,IHYDROGEN_GH2STO)$(RRRAAA(IR,IA) AND (IAGK_HASORPOT(IY411,IA,IHYDROGEN_GH2STO))),VHYDROGEN_STOLOADT(IY411,IA,IHYDROGEN_GH2STO,IS3,T))
   =E=
   IHYDROGEN_DH2_REGION_T_Y(IY411,IR,IS3,T)+
   SUM(IA$RRRAAA(IR,IA),IHYDROGEN_DH2_AREA_T_Y(IY411,IA,IS3,T))
   + SUM(IAGK_HASORPOT(IY411,IA,IHYDROGEN_GH2FUEL) $(RRRAAA(IR,IA)),VGE_T(IY411,IA,IHYDROGEN_GH2FUEL,IS3,T)/GDATA(IHYDROGEN_GH2FUEL,'GDFE'))
   + SUM(IAGK_HASORPOT(IY411,IA,IHYDROGEN_GH2UP) $(RRRAAA(IR,IA)),HYDROGEN_H2TOF(IHYDROGEN_GH2UP)*VGF_T(IY411,IA,IHYDROGEN_GH2UP,IS3,T))
    + SUM(IAGK_HASORPOT(IY411,IA,IHYDROGEN_GH2TOBIOMETH) $(RRRAAA(IR,IA)),VGBIOMETH_T(IY411,IA,IHYDROGEN_GH2TOBIOMETH,IS3,T)/GDATA(IHYDROGEN_GH2TOBIOMETH,'GDFE'))
$include "../../base/addons/_hooks/qhydrogen_eq.inc"

;

* Hydrogen production equal demand on area level:
* Prod + OutputStorage - InputStorage = DemandTransport + DemandPower

QHYDROGEN_EQ_AREA(IY411,IA,IS3,T)$HYDROGEN_DH2(IY411,IA)..
   SUM(IHYDROGEN_GETOH2$IAGK_HASORPOT(IY411,IA,IHYDROGEN_GETOH2),VGE_T(IY411,IA,IHYDROGEN_GETOH2,IS3,T)*GDATA(IHYDROGEN_GETOH2,'GDFE')) +
   SUM(IHYDROGEN_GEHTOH2$IAGK_HASORPOT(IY411,IA,IHYDROGEN_GEHTOH2),VGE_T(IY411,IA,IHYDROGEN_GEHTOH2,IS3,T)*GDATA(IHYDROGEN_GEHTOH2,'GDFE')) +
   SUM(IHYDROGEN_GCH4TOH2$IAGK_HASORPOT(IY411,IA,IHYDROGEN_GCH4TOH2),VHYDROGEN_GH2_T(IY411,IA,IHYDROGEN_GCH4TOH2,IS3,T)) +
   SUM(IHYDROGEN_GH2STO$IAGK_HASORPOT(IY411,IA,IHYDROGEN_GH2STO), VHYDROGEN_GH2_T(IY411,IA,IHYDROGEN_GH2STO,IS3,T))
   - SUM(IHYDROGEN_GH2STO$(IAGK_HASORPOT(IY411,IA,IHYDROGEN_GH2STO)),VHYDROGEN_STOLOADT(IY411,IA,IHYDROGEN_GH2STO,IS3,T))
   =E=
   IHYDROGEN_DH2_AREA_T_Y(IY411,IA,IS3,T)
   + SUM(IHYDROGEN_GH2FUEL$IAGK_HASORPOT(IY411,IA,IHYDROGEN_GH2FUEL),VGE_T(IY411,IA,IHYDROGEN_GH2FUEL,IS3,T)/GDATA(IHYDROGEN_GH2FUEL,'GDFE'))
   + SUM(IAGK_HASORPOT(IY411,IA,IHYDROGEN_GH2TOBIOMETH),VGBIOMETH_T(IY411,IA,IHYDROGEN_GH2TOBIOMETH,IS3,T)/GDATA(IHYDROGEN_GH2TOBIOMETH,'GDFE'))
$include "../../base/addons/_hooks/qhydrogen_eq_area.inc"
;

* Hydrogen production from steam reforming restricted by capacity
QHYDROGEN_GCH4KT(IAGK_HASORPOT(IY411,IA,IHYDROGEN_GCH4TOH2),IS3,T) ..
  IAGK_HASORPOT(IY411,IA,IHYDROGEN_GCH4TOH2)*GKRATE(IA,IHYDROGEN_GCH4TOH2,IS3)
   =G= VHYDROGEN_GH2_T(IY411,IA,IHYDROGEN_GCH4TOH2,IS3,T)
;


QHYDROGEN_STOVOL(IY411,IA,IHYDROGEN_GH2STO,S,T)$(IAGK_HASORPOT(IY411,IA,IHYDROGEN_GH2STO) AND IS3(S)
$ifi %RollingSeasons%==yes $ifi %import_results%==yes $ifi %ADDH2STOVOLTS%==price  AND NOT(CARD(S) EQ ORD(S) AND ORD(T) EQ CARD(T))
)..
    VHYDROGEN_STOVOL_T(IY411,IA,IHYDROGEN_GH2STO,S,T)
  + CHRONOHOUR(S,T)*(VHYDROGEN_STOLOADT(IY411,IA,IHYDROGEN_GH2STO,S,T) - VHYDROGEN_GH2_T(IY411,IA,IHYDROGEN_GH2STO,S,T)/GDATA(IHYDROGEN_GH2STO,'GDFE'))
  + ((VHYDROGEN_STOVOL_T(IY411,IA,IHYDROGEN_GH2STO,S,T) + CHRONOHOUR(S,T)*(VHYDROGEN_STOLOADT(IY411,IA,IHYDROGEN_GH2STO,S,T) - VHYDROGEN_GH2_T(IY411,IA,IHYDROGEN_GH2STO,S,T)/GDATA(IHYDROGEN_GH2STO,'GDFE'))
   - SUM(ITALIAS$(ORD(ITALIAS) EQ 1),VHYDROGEN_STOVOL_T(IY411,IA,IHYDROGEN_GH2STO,S,ITALIAS))
    )*(SSIZE(S)-1))$(ORD(T) EQ CARD(T))
    =E=
       VHYDROGEN_STOVOL_T(IY411,IA,IHYDROGEN_GH2STO,S,T+1) +SUM(ITALIAS$(ORD(ITALIAS) EQ 1),VHYDROGEN_STOVOL_T(IY411,IA,IHYDROGEN_GH2STO,S++1,ITALIAS))$(CARD(T) EQ ORD(T) AND NOT(CARD(S) EQ ORD(S)))
$ifi not %RollingSeasons%==yes $ifi not %import_results%==yes                             +SUM(ITALIAS$(ORD(ITALIAS) EQ 1),VHYDROGEN_STOVOL_T(IY411,IA,IHYDROGEN_GH2STO,S++1,ITALIAS))$(CARD(S) EQ ORD(S) AND ORD(T) EQ CARD(T))
$ifi not %RollingSeasons%==yes $ifi %import_results%==yes $ifi not %ADDH2STOVOLTS%==price  +SUM(ITALIAS$(ORD(ITALIAS) EQ 1),VHYDROGEN_STOVOL_T(IY411,IA,IHYDROGEN_GH2STO,S++1,ITALIAS))$(CARD(S) EQ ORD(S) AND ORD(T) EQ CARD(T))
$ifi     %RollingSeasons%==yes $ifi %import_results%==yes $ifi     %ADDH2STOVOLTS%==cont   +SUM(SSS$(ICOUNTSSSTTT(S,T) EQ ICOUNTSSSTTT(SSS,T)),SUM(ITALIAS$(ORD(ITALIAS) EQ 1),H2STOVOLTS(IY411,IA,IHYDROGEN_GH2STO,S++1,ITALIAS)))$(CARD(S) EQ ORD(S) AND ORD(T) EQ CARD(T))
$include "../../base/addons/_hooks/qhydrogen_stovol.inc"
;

*Hydrogen storage is modelled as a inter-seasonal storage that can be used with a T resolutions
QHYDROGEN_STOMAXCON(IY411,IA,IHYDROGEN_GH2STO,S,T)$(IAGK_HASORPOT(IY411,IA,IHYDROGEN_GH2STO) AND (IS3(S) OR IS3(S--1)))..
     GKFX(IY411,IA,IHYDROGEN_GH2STO)
   + VGKNACCUMNET(IY411,IA,IHYDROGEN_GH2STO)$SUM(Y$(YVALUE(Y) LE YVALUE(IY411)),IAGKNY(Y,IA,IHYDROGEN_GH2STO))
$ifi %DECOM%==yes   -VDECOM_EXO_ACCUM(IY411,IA,IHYDROGEN_GH2STO)$IGDECOMEXOPOT(IY411,IA,IHYDROGEN_GH2STO)
    =G=
   VHYDROGEN_STOVOL_T(IY411,IA,IHYDROGEN_GH2STO,S,T)
;

* Maximum loading, hydrogen gas storage:
QHYDROGEN_STOMAXLOAD(IY411,IA,IHYDROGEN_GH2STO,IS3,T)$(IAGK_HASORPOT(IY411,IA,IHYDROGEN_GH2STO))..
   (   GKFX(IY411,IA,IHYDROGEN_GH2STO)/GDATA(IHYDROGEN_GH2STO,'GDSTOHLOAD')
   + (VGKNACCUMNET(IY411,IA,IHYDROGEN_GH2STO)/GDATA(IHYDROGEN_GH2STO,'GDSTOHLOAD'))$SUM(Y$(YVALUE(Y) LE YVALUE(IY411)),IAGKNY(Y,IA,IHYDROGEN_GH2STO))
$ifi %DECOM%==yes   -(VDECOM_EXO_ACCUM(IY411,IA,IHYDROGEN_GH2STO)/GDATA(IHYDROGEN_GH2STO,'GDSTOHLOAD'))$IGDECOMEXOPOT(IY411,IA,IHYDROGEN_GH2STO)
   ) *(1$(NOT IGKRATE(IA,IHYDROGEN_GH2STO,IS3,T)) + IGKRATE(IA,IHYDROGEN_GH2STO,IS3,T))
    =G= VHYDROGEN_STOLOADT(IY411,IA,IHYDROGEN_GH2STO,IS3,T)
;

* Maximum unloading, hydrogen gas storage:
QHYDROGEN_STOMAXUNLD(IY411,IA,IHYDROGEN_GH2STO,IS3,T)$(IAGK_HASORPOT(IY411,IA,IHYDROGEN_GH2STO))..
   (   GKFX(IY411,IA,IHYDROGEN_GH2STO)/GDATA(IHYDROGEN_GH2STO,'GDSTOHUNLD')
   + (VGKNACCUMNET(IY411,IA,IHYDROGEN_GH2STO)/GDATA(IHYDROGEN_GH2STO,'GDSTOHUNLD'))$SUM(Y$(YVALUE(Y) LE YVALUE(IY411)),IAGKNY(Y,IA,IHYDROGEN_GH2STO))
$ifi %DECOM%==yes   -(VDECOM_EXO_ACCUM(IY411,IA,IHYDROGEN_GH2STO)/GDATA(IHYDROGEN_GH2STO,'GDSTOHUNLD'))$IGDECOMEXOPOT(IY411,IA,IHYDROGEN_GH2STO)
   ) *(1$(NOT IGKRATE(IA,IHYDROGEN_GH2STO,IS3,T)) + IGKRATE(IA,IHYDROGEN_GH2STO,IS3,T))
    =G=
   VHYDROGEN_GH2_T(IY411,IA,IHYDROGEN_GH2STO,IS3,T)
;

*"Capacity adjustment constraint on electricity generation from biogas upgraded plants (MW)"
QHYDROGENGKE_UP_ADD(IY411,IA,IGCOMB1,IS3,T)$(IAGK_HASORPOT(IY411,IA,IGCOMB1) AND SUM(IGCOMB2$GGCOMB(IGCOMB1,IGCOMB2),HYDROGEN_H2TOF(IGCOMB2)))..

  VGE_T(IY411,IA,IGCOMB1,IS3,T)+ SUM(IGCOMB2$GGCOMB(IGCOMB1,IGCOMB2),(VGE_T(IY411,IA,IGCOMB2,IS3,T)*(1+HYDROGEN_H2TOF(IGCOMB2))))
  =L=
  GKFX(IY411,IA,IGCOMB1)+VGKNACCUMNET(IY411,IA,IGCOMB1)
$ifi %DECOM%==yes   -VDECOM_EXO_ACCUM(IY411,IA,IGCOMB1)$IGDECOMEXOPOT(IY411,IA,IGCOMB1)

;

* Biomethane production equals demand on international level in each year:
QBIOMETHANE_EQ(IY411)$(SUM(G,IGBIOMETHANE(G)) AND SUM(G,IHYDROGEN_GH2TOBIOMETH(G)))..
*Production biomethane
    + SUM((C,IA,IS3,T,IHYDROGEN_GH2TOBIOMETH)$(ICA(C,IA) AND IAGK_HASORPOT(IY411,IA,IHYDROGEN_GH2TOBIOMETH)),VGBIOMETH_T(IY411,IA,IHYDROGEN_GH2TOBIOMETH,IS3,T)*IHOURSINST(IS3,T))
   =E=
*Demand biomethane
    SUM((C,IA,IS3,T,IGBIOMETHANE)$(ICA(C,IA) AND IAGK_HASORPOT(IY411,IA,IGBIOMETHANE)),VGF_T(IY411,IA,IGBIOMETHANE,IS3,T)*IHOURSINST(IS3,T))

;

QGKBIOMETHANE_UP(IY411,IA,IHYDROGEN_GH2TOBIOMETH,IS3,T)$(IAGK_HASORPOT(IY411,IA,IHYDROGEN_GH2TOBIOMETH) AND (SUM(Y$(YVALUE(Y) LE YVALUE(IY411)),IAGKNY(Y,IA,IHYDROGEN_GH2TOBIOMETH)) OR IGDECOMEXOPOT(IY411,IA,IHYDROGEN_GH2TOBIOMETH)))..
* Complement to assigning VGE_T.UP for the case with endogenous IGKENOSTO investments.
        VGBIOMETH_T(IY411,IA,IHYDROGEN_GH2TOBIOMETH,IS3,T) =L=
        (GKFX(IY411,IA,IHYDROGEN_GH2TOBIOMETH)
$ifi %DECOM%==yes   -VDECOM_EXO_ACCUM(IY411,IA,IHYDROGEN_GH2TOBIOMETH)$IGDECOMEXOPOT(IY411,IA,IHYDROGEN_GH2TOBIOMETH)
        +VGKNACCUMNET(IY411,IA,IHYDROGEN_GH2TOBIOMETH)$SUM(Y$(YVALUE(Y) LE YVALUE(IY411)),IAGKNY(Y,IA,IHYDROGEN_GH2TOBIOMETH)))*(1$(NOT IGKRATE(IA,IHYDROGEN_GH2TOBIOMETH,IS3,T)) + IGKRATE(IA,IHYDROGEN_GH2TOBIOMETH,IS3,T))
;
